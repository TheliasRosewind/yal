package analyse;

import java.util.*;
import arbre.* ;
import arbre.expressions.* ;
import arbre.expressions.binaires.* ;
import arbre.expressions.binaires.logiques.* ;
import arbre.expressions.binaires.arithmetiques.* ;
import arbre.instructions.* ;
import arbre.instructions.io.*;
import exceptions.AnalyseSyntaxiqueException;
import tds.TDS;
import tds.entrees.*;
import tds.symboles.*;
import tds.types.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL, LIRE ;
terminal String    IDF, CSTENTIERE, TYPE ;
   
/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, INST, ECR, AFF, LDECL, DECL, LIR;
non terminal Expression EXP ;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME  IDF  DEBUT LDECL:ld FIN
                {: RESULT = ld ; :}
                |
                PROGRAMME  IDF  DEBUT LINST:li FIN
                {: RESULT = li ; :}
                ;

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
                |
                INST:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

INST    ::=     ECR:e POINTVIRGULE
                {: RESULT = e ; :}
                |
                AFF:i POINTVIRGULE
                {: RESULT = i ;:}
                |
                DECL:d POINTVIRGULE
                {: RESULT = d ; :}
                |
                LIR:l POINTVIRGULE
                {: RESULT = l ; :}
                ;

ECR     ::=     ECRIRE EXP:e
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
                ;

AFF     ::=     IDF:i EGAL EXP:e
                {: RESULT = new Affectation(i, e, ileft + 1) ; :}
                ;

LDECL   ::=     LDECL:ld  DECL:d
                {: ((BlocDInstructions)ld).ajouter(d) ;
                  RESULT = ld ; :}
                |
                DECL:d
                {: BlocDInstructions b = new BlocDInstructions(dleft + 1) ;
                   b.ajouter(d) ;
                   RESULT = b ; :}
                ;

DECL    ::=     TYPE:t IDF:i
                {: int dep = TDS.getInstance().newDeplacement() ;
                   TDS.getInstance().ajouter(new EntreeVariable(i, ileft + 1), new SymboleVariable(dep)) ;
                   RESULT = new Declaration(t, i, ileft + 1) ; :}
                ;

LIR     ::=     LIRE IDF:i
                {: RESULT = new Lire(i,ileft+1) ; :}
                ;

EXP     ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
                |
                IDF:i
                {: RESULT = new Idf(i, ileft + 1) ; :}
                ;